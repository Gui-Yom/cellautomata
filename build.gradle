plugins {
    id 'java-library'
    id 'maven'
}

repositories {
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    implementation 'com.madgag:animated-gif-lib:1.4'
    implementation 'com.github.zhuker:apng4j:0c4ded26b0'
    testImplementation 'junit:junit:4.12'
}

group = "com.limelion"
version = "1.2.0"

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

task fatjar(type: Jar) {
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task buildPackage(type: DefaultTask) {
    dependsOn fatjar
    dependsOn sourcesJar
    dependsOn javadocJar
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

clean.doFirst {
    delete "output"
}
